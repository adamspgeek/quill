buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

// don't lint for every build (run it manually when you want it to run)
tasks.whenTaskAdded { task ->
    if (task.name.equals('lint')) {
        task.enabled = false
    }
}


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'me.vickychijwani.spectre'
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1_005_005      // 3 digits each for major, minor, patch
        versionName '1.5.5'

        // enable support for vector drawables on API < 21
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    signingConfigs {
//        release {
//            storeFile(file(System.getenv('KEYSTORE') ?: '') ?: '')
//            storePassword(System.getenv('KEYSTORE_PASSWORD') ?: '')
//            keyAlias(System.getenv('KEY_ALIAS') ?: '')
//            keyPassword(System.getenv('KEY_PASSWORD') ?: '')
//        }
//    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            ext.enableCrashlytics = false
        }
        release {
//            signingConfig signingConfigs.release
            minifyEnabled false   // FIXME disabled because login, revoke token strangely stopped working
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
        }
    }
    lintOptions {
        // check for lint errors but don't abort builds
        // I had to disable it because we don't get translations for new strings immediately
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

// Fabric / Crashlytics keys are mandatory for release builds
task checkFabricPropertiesExists << {
    if (!file('fabric.properties').exists()) {
        throw new GradleException('Missing fabric.properties file for Fabric / Crashlytics!')
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn checkFabricPropertiesExists
    }
}

dependencies {
    // reactive extensions (rx)
    compile "io.reactivex.rxjava2:rxjava:2.0.6"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"

    // networking / communication / data storage
    compile("com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion") {
        exclude module: "okhttp"
    }
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttpVersion"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"
    compile "com.squareup:otto:1.3.6"

    // UI
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibVersion"
    compile "com.android.support:percent:$rootProject.ext.supportLibVersion"
    compile "com.android.support:customtabs:$rootProject.ext.supportLibVersion"
    compile "org.ocpsoft.prettytime:prettytime:4.0.0.Final"
    compile "com.tsengvn:typekit:1.0.1"

    // misc
    compile "com.jakewharton:butterknife:7.0.0"
    compile("com.crashlytics.sdk.android:crashlytics:2.6.2@aar") {
        transitive = true
    }
    compile("com.crashlytics.sdk.android:answers:1.3.7@aar") {
        transitive = true
    }
    compile "com.github.hotchemi:permissionsdispatcher:$rootProject.ext.permissionsDispatcherVersion"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$rootProject.ext.permissionsDispatcherVersion"
    compile "com.github.slugify:slugify:2.1.3"

    // debug
    debugCompile "com.squareup.leakcanary:leakcanary-android:1.5"
    debugCompile "com.jakewharton.scalpel:scalpel:1.1.2"
    debugCompile "io.palaima.debugdrawer:debugdrawer:0.7.0"
    debugCompile "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.debugdrawerVersion"
    debugCompile "io.palaima.debugdrawer:debugdrawer-okhttp3:$rootProject.ext.debugdrawerVersion"
    debugCompile "io.palaima.debugdrawer:debugdrawer-picasso:$rootProject.ext.debugdrawerVersion"
    debugCompile "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.debugdrawerVersion"
    debugCompile "com.facebook.stetho:stetho:1.4.2"
    debugCompile "com.facebook.stetho:stetho-okhttp3:1.4.2"
    debugCompile "com.uphyca:stetho_realm:2.0.0"

    // test
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.json:json:20140107"    // don't depend on Android's JSONObject: http://stackoverflow.com/a/30759769/504611
    testCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okhttpVersion"
}
